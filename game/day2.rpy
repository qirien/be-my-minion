label day2:
    scene bg bedroom dusk with fade
    play music cyril_theme
    "Is that knocking?! Who could be brazen enough to wake me up so early?!"
    p "Niir! I am in no mood for your games! Begone!"
    c "Please, Princess, it is I, Cyril, and I only wanted to-"
    p "Moronious?! This had better be important!"
    show cyril hat neutral at center with moveinleft
    c "Good.  Good.  I'm glad that you opened up."
    show cyril hat concerned blush at basicfade
    c "I don't want you to think that I'm - well, disapproving."
    c "But some information has come to my attention."
    show cyril hat concerned blush eyes closed at basicfade
    c "You were {b}banished{/b}, Princess!"
    p "I am well aware of that.  So what are you bothering me for?"
    show cyril hat surprised at basicfade
    c "It's just- you were {b}banished{/b}, Princess!"
    p "If you're going to waste my time like a stuttering fool Moronious, I suggest you begone!"
    show cyril hat smile at basicfade
    c "Well, it wouldn't be the first time that you've commanded me to begone."
    show cyril hat concerned at basicfade
    c "Anyway, please tell me this is a mistake!  It must be a mistake!"
    c "I'm sure that the kingdom is just overreacting.  Playing a joke perhaps? Like Niir?"
    show cyril hat concerned eyes closed at basicfade
    c "There is no way they would actually b-b-banish you!"
    p "Oh, they did banish me.  And that's why I'm here, to get justice."
    p "And {b}you{/b} will be part of those plans."
    show cyril hat surprised at basicfade
    c "Erm-ahem!  Yes.  I don't think that is a good idea- it is already dangerous enough to have a disgraced Princess-"
    p "You don't {b}want{/b} me here?"
    "Who does he think he is?  I'm not the one at fault here!  I'd like to {b}see{/b} him try to kick me out!"
    show cyril hat concerned blush at basicfade
    c "No! I didn't say that."
    show cyril hat concerned at basicfade
    c "I'm just {i}concerned{/i} Princess."
    show cyril hat concerned eyes closed at basicfade
    c "If you have serious allegations before you then ah-"
    p "You're ashamed of me being in your presence, then?"
    show cyril hat concerned blush at basicfade
    c "No!  I love you being in my presence."
    show cyril hat concerned blush eyes closed at basicfade
    c "{i}I do{/i}.  It's just..."
    show cyril hat concerned at basicfade
    c "I'm afraid for you and the errr... connotations that are attached to that."
    p "I can handle myself Moronious."
    p "And when I put things right you will see that for yourself."
    show cyril hat neutral at basicfade
    c "As long as I'm not seen as {i}aiding and abetting{/i}."
    p "If you are not willing to make sacrifices for your future Queen, then what good are you?"
    show cyril hat concerned blush at basicfade
    c "I am, I am.  Forgive me your Highness."
    p "I forgive you, you foolish mage.  Just don't doubt me again."
    p "Oh, and one more thing."
    show cyril hat surprised at basicfade
    c "Yes, Highness?"
    p "You will join me for dinner in the kitchens. Tonight."
    show cyril hat concerned blush eyes closed at basicfade
    c "Y-yes, of course!"
    hide cyril at center with moveoutleft
    
    show niir neutral at right with moveinright
    show niir smirk at basicfade
    n "You were banisshhed? Your position musst be desssperate, then."
    p "How long have you been eavesdropping?!"
    show niir mischief at basicfade
    n "Long enough to hear you sssnorrrring. Mossst unladylike."
    p "Moronious!"
    show cyril hat neutral at midleft with moveinleft
    show cyril hat surprised at basicfade
    c "Yes, Princess? Oh! What's {b}he{/b} doing here?!"
    p "That was my question. I would have thought that a capable mage would have this room warded against unwanted intrusions."
    show niir neutral at basicfade
    n "If you think Cccyril is capable--"
    show cyril hat concerned at basicfade
    c "I did! It was! But- oh, the spell's locus...I had it tied to me, not the room! What a dreadful oversight."
    show cyril hat concerned blush at basicfade
    c "I am terribly, terribly sorry, Princess! I shudder to think what these terrible dragons might have tried to do if I had not fixed the ward."
    p "Yes, well, it should have been fixed in the first place!"
    show niir frown at basicfade
    n "Disssapointing. I had ssso many plansss..."
    show cyril hat angry at basicfade
    c "{font=fonts/ankecallig-fg.ttf}Protaegis! Protaegis Cubiculum!{/font}"
    show cyril hat neutral with magic_flash
    show cyril hat neutral at basicfade
    c "Now I've made two spells - one tied to you, and one to the room. The locus for the room spell is the door, so if you open the door, the spell will allow people through."
    show cyril hat concerned at basicfade
    c "I've also added a spell on your person, that creates an anti-dragon barrier. I don't know why I didn't do this in the first place, it must have slipped my mind."
    show niir neutral at basicfade
    n "Like ssso many other thingsss."
    p "So dragons cannot touch me now? What would happen if they tried?"
    show cyril hat smile at basicfade
    c "They can only touch you if you reach out to touch them first. As for what would happen... ahem! Let's just say they would regret such an action!"
    p "Hmm. What a powerful spell. Would you like to test it, Niir?"
    show niir angry at basicfade
    n "I have tesssted that ssspell before. I have no desssire to experrrience it again."
    p "I see. Thank you, Moronious; you may go."
    show cyril hat concerned blush at basicfade
    c "P-princess, I don't think it's wise to be alone with this, this..."
    p "Do you not trust your own spell? Or is it {b}I{/b} you do not trust?"
    show cyril hat concerned blush eyes closed at basicfade
    c "No...I trust you, just- please, be careful!"
    hide cyril at midleft with moveoutleft
    
    p "Do you have business with me, dragon?"
    show niir smile at basicfade
    n "Yesss, Balrung wished to ssspeak with you."
    p "Well, where is he? Why doesn't he just come knock on my door?"
    show niir determined at basicfade
    n "I think he jussst likesss to send me on errandsss."
    p "So, you weren't really watching me sleep for hours...?"
    show niir mischief at basicfade
    n "I'll let you wonderrr about that one."
    p "Hmph. I won't be losing sleep over it. But, as long as you're here..."
    show niir smirk at basicfade
    n "Yesss?"
    p "Join me for dinner tonight. In the kitchen."
    show niir mischief at basicfade
    n "I'll be therrre."
    hide niir at center with moveoutright
    
    p "I suppose I'll see what Balrung wants..."
    if (asked_scepter == "Balrung"):
        "Perhaps he has found the scepter?!"
        
    scene bg dungeon with fade
    show balrung neutral at center, basicfade
    play music balrung_theme
    p "Balrung! I don't appreciate being summoned to meet with {b}you{/b}! Next time you will come and meet with me!"
    show balrung smile at basicfade
    b "My apologies, my lady, I didn't think it appropriate to disturb a lady in her bedchamber so early in the morning."
    p "But you thought it appropriate to send the lascivious Niir to my bedchamber?!"
    show balrung smirk at basicfade
    b "I did not send him. I merely mentioned that I hoped I would get a chance to talk with you today, and he departed."
    if (asked_scepter == "Balrung"):
        p "Well, what is it? Have you found the scepter?"
        show balrung determined at basicfade
        b "No. It seems to be somewhere nearby, but I cannot discern its exact location yet."
    else:
        p "Well, here I am. Speak."
    
    show balrung neutral at basicfade
    b "I understand you are not here entirely of your own free will. That you were banished."
    p "A temporary state of affairs, I assure you."
    show balrung determined at basicfade
    b "Believe me, Princess, when I say that I understand completely. I know all too well how it feels to make one lapse in judgement, and have inordinate punishment dealt to you."
    p "You... understand?"
    show balrung angry at basicfade
    b "Those who fear power are often overly vindictive against those who use it."
    p "Vindictive, yes. Have you met my sister, Magnolia?"
    show balrung determined at basicfade
    b "No, but I knew someone quite like her, once. Afraid of my power."
    p "Self-righteous."
    show balrung angry at basicfade
    b "Ambitious."
    p "Narcissistic."
    show balrung determined at basicfade
    b "Self-sacrificing."
    p "Knows whats best for everyone else."
    show balrung neutral at basicfade
    b "Yes, precisely. So if you are in need of allies here, I hope you will seek me out, and perhaps we can aid each other."
    p "But you could only aid me if you were freed."
    show balrung smile at basicfade
    b "Well, I would certainly have much greater power as a free dragon. But I am willing to do what little I can for you, in this form, as well."
    p "I shall consider your offer, dragon."
    show balrung smirk at basicfade
    b "Please do."
    p "But first, I have a simple request of you."
    show balrung neutral at basicfade
    b "Yes?"
    p "Come to the kitchen this evening and dine with me."
    show balrung smile at basicfade
    b "I'd be honored, Highness."
    scene black with fade
    "And now, for dinner...A little test, to see who will be the most useful to me..."
    
label dinner_party:
        
    scene bg kitchen with fade
    show niir neutral at right
    show balrung neutral at center, basicfade
    show cyril neutral hat at left, basicfade
    play music cyril_theme
    
    p "Good, you are all here. That proves you can fulfill a simple request."
    show balrung smirk at basicfade
    b "It has been a long time since I attended a dinner party."
    show niir frown at basicfade
    n "I hope the food issss better than usssual."
    p "Yes, a dinner party requires food. Moronious? Isn't that your job?"
    show cyril hat surprised at basicfade
    c "R-right! But, surely you, you don't want to eat with these...creatures?"
    show niir smile at basicfade
    n "She sssssummoned usss here.  Sssso clearly sshe doesss."
    p "Yes, yes.  What that Niir dragon said.  I need all of you present."
    show cyril hat concerned at basicfade
    c "I just thought that when you requested dinner-"
    show balrung smile at basicfade
    b "You assumed she wanted be alone, with you, over a candlelit meal? That's rather presumptuous, Merlonious."
    show niir mischief at basicfade
    n "Hilarioussss, don’t you think?"
    show cyril hat concerned blush at basicfade
    c "That wasn’t- it wasn’t entirely like that.  You must believe me Princess."
    show cyril neutral hat at left
    p "I don’t care what you thought.  Your future Queen hasn’t the time to consider such things.  What a waste. "
    p "No, I thought it would be nice for us to...{i}talk{/i} together about matters of the kingdom."
    p "{b}Food{/b}, Moronious!"
    show cyril hat neutral at basicfade
    c "Right on it!  I apologize your majesty."
    show cyril hat angry at basicfade
    c "{font=fonts/ankecallig-fg.ttf}Vittus Cottura{/font}!"
    show cyril hat concerned with magic_flash
    p "Honestly, not {b}that{/b} again Moronious.  Is that all you know how to summon?"
    show cyril hat concerned at basicfade
    c "I’ll try something else then."
    show niir concerned at basicfade
    n "We’re waitingggg."
    show cyril hat concerned eyes closed at basicfade
    c "Do be quiet, dragon, you are making me lose my concentration."
    show cyril hat angry at basicfade
    c "{font=fonts/ankecallig-fg.ttf}Vittus Cottura Splendida{/font}!"
    show cyril hat concerned with magic_flash
    # TODO: magic vfx
    show balrung angry at basicfade
    b "I am convinced that Merlonious summons this pitiful human food as part of our punishment. Dragons were never meant to be herbivorous."
    show cyril hat concerned at basicfade
    c "I-it's good for you!"
    show niir sad at basicfade
    n "Ohhh, what I wouldn't give for a bit of deer, or mutton, or even ssssquirrel!"
    show balrung neutral at basicfade
    b "Elk..."
    show niir neutral at basicfade
    n "Turrrkey..."
    show balrung smirk at basicfade
    b "Bison..."
    show niir smile at basicfade
    n "Rrrrabbit!"
    show cyril hat angry at basicfade
    c "Enough complaining, both of you."
    p "Yes, anyway, I brought you all here together in order to discuss something."
    p "It has come to my attention that all of you now know about my purported banishment." 
    show cyril hat surprised at basicfade
    c "Purported?"
    p "Quiet, Moronious!  As I was saying... you all know the true reason why I am here.  And so, you all must realize by now that I have been cheated out of my destiny by my own blood."
    show balrung smile at basicfade
    b "Destiny is an unfaithful mistress..."
    show niir mischief at basicfade
    n "Desssstiny, you ssssay?"
    p "I'm sure you all realize that if you were to align with my sister, you would be no friend of [k_name].  So it would be wise of all of you to work out where your allegiance lies."
    show niir determined at basicfade
    n "Dragonsss have no allegiancccce." 
    show balrung smirk at basicfade
    b "What advantages would an ally of yours gain, I wonder? And what would your sister do with this prison?"
    show cyril hat surprised at basicfade
    c "No, no, no, no, no, no.  We cannot be talking about this, Princess.  These are political matters.  This is not up for discussion over dinner."
    p "Oh please, mage.  I think we’re all adults here.  Surely we can discuss the future of the kingdom together without any..."
    show niir mischief at basicfade
    n "Or we could dissscusss other forbidden topicsssss..."
    show niir frown at basicfade
    show balrung determined at basicfade
    b "That would be like the blind man describing a sunset in your case, Niir. No, let the Princess speak."
    show cyril hat angry at basicfade
    c "I warn you, speaking of things like this never go well.  "
    c "My, word might even get out that we’re {i}conspiring{/i}!  Conspiring, Princess!  I think it would be best if we leave this alone for the present moment."
    p "Charges of conspiracy only matter if we lose. No one charges Queens with conspiracy!"
    show cyril hat concerned eyes closed at basicfade
    c "It’s not the charge itself.  It’s the principle.  I would rather not take part in such a discussion."
    show balrung determined at basicfade
    b "Leave, then, and let the adults discuss matters too weighty for your simplistic morals."
    show niir smirk at basicfade
    n "Ssssimplistic Cccyril.  What ssshall we do with you?"
    show cyril hat concerned at basicfade
    c "I don’t think we need to do any-"
    show niir mischief at quarterleft with quickmove
    show niir at right with quickmove
    show cyril concerned blush at basicfade
    c "Excuse me, dragon!  Just what do you think you’re...."
    show niir happy at basicfade
    n "Hahahaha.  Oh, look at that.  Hissss faccce is red."
    show cyril concerned at basicfade
    c "A hat is a rather serious piece of a magical wardrobe.  So I demand that you give that back."
    show cyril concerned eyes closed at basicfade
    c "This instant, Niir!"
    show niir neutral at basicfade
    n "Thissss inssstant, you sssay?"
    show niir at center
    show balrung neutral at right
    with move
    show cyril angry at basicfade
    c "Don’t you dangle that thing in front of me, Niir.  This is no joking matter."
    show cyril concerned at basicfade
    c "And no, I will not jump for it.  I will not sink to such standards."
    show balrung smirk at basicfade
    b "Princess, it looks like you and I might need to discuss matters on our own, later, away from the squabbling children."
    p "Perhaps. For now, I shall simply enjoy the evening's entertainment."
    show niir mischief at basicfade
    n "How about you fetch?  Insssstead of jumping."
    show cyril angry at basicfade
    c "Don’t you dare throw that Ni-"
    show niir neutral at quarterleft with quickmove
    show niir at center with quickmove
    show cyril concerned eyes closed at basicfade
    c "He threw it.  Of course he did.  "
    show cyril concerned at basicfade
    c "Stay right here dragons, while I come right back.  Please keep an eye on them Princess."
    hide cyril at left with moveoutleft
    "Oh, I'll keep an eye on them, alright. But, which one?"
    menu:
        "Talk to Balrung":
            $ balrung_affection += 1
            p "Balrung, you should know that my sister Magnolia is not a friend to dragons."
            show balrung neutral at basicfade
            b "Why do you say that?"
            p "She's been trying to convince the United Council to sign new legislation forbidding dragons from holding titles." 
            show balrung smirk at basicfade
            b "Are there many dragons attempting to hold titles?"
            p "Oh, yes, it's quite the craze to have a dragon consort. "
            p "Princess Dianthus from two kingdoms over wanted to make hers King-Consort, but the United Council hasn't acquiesed yet."
            show balrung determined at basicfade
            b "And what do you think they should do?"
            p "Human, dragon, I don't really care, as long as I'm the Queen!"
            show balrung smile at basicfade
            b "Interesting."            
            show cyril hat concerned at left with moveinleft
            
        "Flirt with Niir":
            $ niir_affection += 1
            p "Now that we have gotten rid of that {i}inconvenience{/i}, we can get back to business."
            p "Personally, I've enjoyed your antics, Niir; you are quite the asset to have on hand."
            show niir smirk at basicfade
            n "I would like to have my handsss on your-"
            show balrung smile at basicfade
            b "You were asking for that one, Princess."
            p "Of course I was. And Niir was kind enough to deliver."
            show niir frown at basicfade
            n "Kind?! You'rrrre suppossssed to be outraged!"
            p "Why would I be outraged when you said exactly what I predicted? That wouldn't be logical, would it?"
            show balrung smile eyes closed at basicfade
            b "Oh ho ho! Careful with this one, Niir."
            show niir neutral at basicfade
            n "Perhapssss I should try harder to sssurprise you."
            "That's exactly what I'm hoping for..."
            p "You can try, but I don’t believe it will be possible."
            show niir determined at basicfade
            n "Nexxxxt time, Princessss."
            show cyril hat concerned at left with moveinleft

        "Help Cyril":
            $ cyril_affection += 1
            scene bg stairs night with fade
            show cyril neutral at midright, basicfade
            p "Wait a moment, Moronious.  I’ll get that hat of yours."
            p "We wouldn’t want you to trip down the stairs doing so.  Actually, I’m not so sure that wasn’t what Niir had in mind."
            show cyril concerned blush at basicfade
            c "Oh, thank you.  I’m most honored.  But I could not have you handling my hat yourself."
            p "And why not?  Is your hat too precious for mere royalty to touch?"
            show cyril smile blush at basicfade
            c "Oh no.  That wasn’t what I meant at all.  It’s just, well, hats are a rather personal thing and I’m not sure if it would be strange suggesting-"
            p "Don’t be ridiculous, Moronious.  It’s only a hat."
            show cyril laugh at basicfade
            c "Ah, yes.  You are right with that one.  Only a hat after all." 
            p "There it is."
            show cyril hat smile blush at basicfade
            c "I am most grateful."
            p "Do stop smiling like a nitwit.  Your desperate plea for pitying is starting to disgust me."
            show cyril hat neutral
            show cyril hat concerned blush at basicfade
            c "Oh, of course.  I wouldn’t dream of disgusting you."
            show cyril hat concerned at basicfade
            c "We should go back. I’m not sure what mess those dragons have gotten into already.  I dare say the food won’t still be there."
            scene bg kitchen 
            show balrung neutral at center
            show niir neutral at right
            with fade
            show cyril hat neutral at left with moveinleft

    p "Thank you all for an...informative evening. My apologies for the ineptitude of the chef."
    show balrung smile at basicfade
    b "Thank {b}you{/b}, Princess. Your presence is a delightful change from the ordinary events of this wretched place."
    show niir smirk at basicfade
    n "At leasssst the cccchef provided usss with entertainment."
    show cyril hat angry at basicfade
    c "Nobody was impressed by that, Niir."
    p "I found it amusing. But now, we should get back to eating this horrid concoction that Moronious conjured."
    show niir frown at basicfade
    n "Disssgusting."
    show cyril hat concerned blush at basicfade
    c "It is perfectly healthy and nutritious!"
    p "The things a princess must put up with for the future of her kingdom..."
    return
